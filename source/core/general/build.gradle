apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

defaultAndroidModule()

android {

    defaultConfig {

        File apikeysFile = file("$rootProject.projectDir/signing/apikeys.properties")

        if (apikeysFile.exists()) {

            Properties apikeysProperties = new Properties()
            apikeysProperties.load(new FileInputStream(apikeysFile))

            String fabricApiKey = apikeysProperties['fabric.apiKey']
            if (fabricApiKey == null) {
                throw new IllegalStateException("fabric.apiKey not provided.")
            }

            manifestPlaceholders += [fabricApiKey: fabricApiKey]

            println(":core: Use real fabric api key")

        } else {

            manifestPlaceholders += [fabricApiKey: "api_key_stub"]

            println(":core: Use fabric api key stub")

        }

    }

}

dependencies {

    deps.shortcut.kotlin()
    deps.shortcut.rx()
    deps.shortcut.dagger(true)

    api deps.debug.timber

    implementation(deps.debug.crashlytics) {
        transitive = true
    }

}